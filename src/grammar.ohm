Storymatic {
  Script = TopLevelStatement*

  WrappedStatementBlock = Wrapped<Statement*>
  WrappedScriptBlock = Wrapped<TopLevelStatement*>
  Indented<Element> = indent Element dedent
  Wrapped<Element> = "{" Element "}" | indent Element dedent | "{" Indented<Element> "}"

  SingleStatementBlock<prefix>
    = WrappedStatementBlock
    | prefix #space Statement -- single_statement

  UnprefixedSingleStatementBlock
    = WrappedStatementBlock
    | Statement -- single_statement

  IfStatement
    = ("if" | "unless") #postWord Expression SingleStatementBlock<"then">
      ("else" #postWord (UnprefixedSingleStatementBlock | IfStatement))?

  SwitchStatement
    = "switch" #space Expression "{" CaseStatement* DefaultStatement? "}"
    | "switch" #space Expression indent CaseStatement* DefaultStatement? dedent

  CaseClause = "case" #space NonemptyListOf<Expression, ","> CaseTerminator
  CaseStatement = CaseClause+ UnprefixedSingleStatementBlock
  DefaultStatement = "default" #space UnprefixedSingleStatementBlock

  CaseTerminator
    = #colonTerminator -- terminator
    | &("{" | indent) -- final

  TryStatement = "try" #postWord UnprefixedSingleStatementBlock CatchStatement? FinallyStatement?
  CatchStatement = "catch" #(postWord identifier)? WrappedStatementBlock?
  FinallyStatement = "finally" #postWord UnprefixedSingleStatementBlock?

  EnumStatement = ("export" #space)? "enum" #space identifier "{" EnumMember* "}"

  EnumMember
    = (word | string_non_interpolatable) #expressionTerminator -- auto_assign
    | (word | string_non_interpolatable) "=" Expression #expressionTerminator -- assigned

  TopLevelStatement
    = Statement
    | "import" #postWord importLocation #statementTerminator -- empty_import
    | "import" (#space "type")? "{" NonemptyListOf<Importable, ","> "}" "from" #postWord importLocation #statementTerminator -- import
    | "import" "*" "as" #postWord identifier "from" #postWord importLocation #statementTerminator -- import_all
    | "import" (#space "type")? #postWord identifier #postWord "from" #postWord importLocation #statementTerminator -- import_default
    | "export" #statementTerminator -- empty_export
    | "export" (#space "type")? "{" NonemptyListOf<Exportable, ","> "}" "from" #postWord importLocation #statementTerminator -- export_from
    | "export" "*" "from" #postWord importLocation #statementTerminator -- export_all_from
    | "export" (#space "type")? "{" NonemptyListOf<Exportable, ","> "}" #statementTerminator -- export
    | "export" #postWord "default" #postWord Expression #statementTerminator -- export_default
    | ExportedVariableAssignment #statementTerminator -- export_variable

  Statement
    = IfStatement
    | TryStatement
    | ClassDeclaration
    | TypeDeclaration
    | InterfaceDeclaration
    | NamespaceDeclaration
    | SwitchStatement
    | EnumStatement
    | ("while" | "until") #postWord Expression SingleStatementBlock<"then"> -- while
    | "for" #postWord ("await" #postWord)? (Assignable "of" #postWord)? Expression SingleStatementBlock<"then"> -- for
    | "rescope" #space NonemptyListOf<Rescopable, ","> #statementTerminator -- rescope
    | "rescope" #space VariableAssignment #statementTerminator -- rescope_assign
    | TypedVariableAssignment -- typed_assignment
    | ~("{" "}") TopLevelIfExp #statementTerminator -- expression

  Rescopable
    = identifier ":" Type -- with_type
    | identifier -- identifier

  ClassDeclaration
    = ("export" #space)? "class" #space identifier GenericTypeParameterList?
      (#space "extends" #space (Extendable | Expression))?
      (#space "implements" #space NonemptyListOf<Implementable, ",">)?
      "{" ClassElement* "}"

  ClassElement
    = ClassProperty<""> #statementTerminator -- property
    | ClassProperty<"@"> #statementTerminator -- static_property
    | IndexSignatureType<""> #statementTerminator -- index_signature
    | IndexSignatureType<"@"> #statementTerminator -- static_index_signature

  importLocation
    = (~(space | "{" | "\\" | "\"" | "\'" | statementTerminator) any)+ &statementTerminator -- filename
    | string_non_interpolatable

  Importable = ("type" #space)? word (#space "as" #space word)?
  Exportable = ("type" #space)? word (#space "as" #space word)?

  statementTerminator
    = spaces ";" -- semicolon
    | terminator

  expressionTerminator
    = spaces "," -- comma
    | terminator

  typeTerminator
    = spaces "," -- comma
    | spaces ";" -- semicolon
    | terminator

  colonTerminator
    = spaces ":" -- colon
    | terminator

  terminator
    = spaces &("}" | "]" | ")" | dedent | end | reserved_block)
    | whitespace? "\n"
    | whitespace? "#"

  TopLevelIfExp
    = Indented<TopLevelIfExp>
    | TopLevelWhileExp #(~lineBreak) ("if" | "unless") #postWord Expression -- if
    | TopLevelWhileExp

  TopLevelWhileExp
    = Indented<TopLevelWhileExp>
    | TopLevelForExp #(~lineBreak) ("while" | "until") #postWord Expression
      ("when" #postWord Expression)? -- while
    | TopLevelForExp

  TopLevelForExp
    = Indented<TopLevelForExp>
    | TopLevelExp #(~lineBreak) "for" #postWord ("await" #postWord)? (Assignable "of" #postWord)? Expression  
      ("when" #postWord Expression)? -- for
    | TopLevelExp

  TopLevelExp
    = Indented<TopLevelExp>
    | "break" -- break
    | "continue" -- continue
    | "return" (#postWord AssignmentExp)? -- return
    | "throw" #postWord AssignmentExp -- throw
    | Expression -- expression

  Expression = IfExp
  NonLoopExpression = AssignmentExp

  LiteralExp
    = Indented<LiteralExp>
    | Function
    | JSXElement
    | "(" Expression ")" -- parenthesized
    | "[" ListOf<ArrayEntry, #expressionTerminator> ","? "]" -- array
    | "{" ListOf<ObjectEntry, #expressionTerminator> ","? "}" -- object
    | "[" indent ListOf<ArrayEntry, #expressionTerminator> ","? dedent "]" -- array_indented
    | "{" indent ListOf<ObjectEntry, #expressionTerminator> ","? dedent "}" -- object_indented
    | "@" ~(id_continue | "[" | ".") -- self
    | "%" -- topic_token
    | "with" #postWord Expression SingleStatementBlock<"then"> -- with
    | "do" #postWord Expression -- do
    | LiteralExp_object_implied
    | InlineClassDeclaration
    | decimalNumber
    | unitNumber
    | hexNumber
    | bigint
    | identifier
    | Property
    | undefined
    | boolean
    | string
    | null
    | Statement -- statement

  LiteralExp_object_implied
    = NonemptyListOf<ObjectEntry_implied, ("," | #lineBreak)>

  InlineClassDeclaration
    = "class" GenericTypeParameterList?
      (#space "extends" #space (Extendable | Expression))?
      (#space "implements" #space NonemptyListOf<Implementable, ",">)?
      "{" ClassElement* "}"

  ArrayEntry
    = Expression
    | "..." Expression -- spread_operator

  Function
    = (GenericTypeParameterList? "(" ParameterList? ")"
      (":" FunctionReturnType)?)? ("->" | "=>") (#(~(lineBreak ~indent)) FunctionBody)?

  ClassProperty<prefix>
    = PrivacyLevel ("readonly" #postWord)? prefix MethodName ("?" | "!")? (":" Type)? ("=" Expression)?

  PrivacyLevel
    = "public" #postWord -- public
    | "protected" #postWord -- protected
    | "private" #postWord -- private
    | -- none

  FunctionBody
    = Expression -- expression
    | WrappedStatementBlock

  MethodName
    = word -- identifier
    | string -- computed_string_key
    | decimalNumber -- numerical_key
    | "[" Expression "]" -- computed_key

  ObjectEntry
    = MethodName ":" Expression -- key_value
    | "..." Expression -- spread_operator
    | identifier -- restructure

  ObjectEntry_implied = MethodName ":" NonLoopExpression

  MemberAccessExp
    = Indented<MemberAccessExp>
    | MemberAccessExp #(~lineBreak) GenericTypeArgumentList
      #(~lineBreak) "(" ArgumentList ")" -- function_call
    | MemberAccessExp "?" GenericTypeArgumentList "(" ArgumentList ")" -- optional_chaining_function_call
    | MemberAccessExp #(whitespace ~(lineBreak | "#" | "(" | "<" ~id_continue)) ImpliedCallArgumentList -- implied_call
    | MemberAccessExp #lineBreak Indented<LiteralExp_object_implied> -- implied_call_object
    | MemberAccessExpNonCall<MemberAccessExp>

  MemberAccessExpNonCall<Child>
    = Indented<MemberAccessExpNonCall<Child>>
    | Child "." word -- member_access
    | Child "?." word -- optional_chaining_member_access
    | Child indent "." word dedent -- member_access_indented
    | Child indent "?." word dedent -- optional_chaining_member_access_indented
    | Child #"?"? #"[" Expression? ".." Expression? "]" -- array_slice
    | Child #"?"? #"[" indent Expression? ".." Expression? dedent "]" -- array_slice_indented
    | Child #"?"? #"[" Expression "]" -- computed_member_access
    | ~("\"" | "'") Child #string -- tagged_template_literal
    | Child "!" ~"=" -- non_null_assertion
    | Child "as" #postWord Type -- as_expression
    | "new" #postWord NCMemberAccessExp #(~lineBreak) GenericTypeArgumentList
      #(~lineBreak) "(" ArgumentList ")" -- class_creation_symbolic
    | "new" #postWord NCMemberAccessExp #(whitespace
      ~(lineBreak | "#" | "(" | "<" ~id_continue)) ImpliedCallArgumentList -- class_creation_implied
    | "new" #postWord NCMemberAccessExp
      #lineBreak Indented<LiteralExp_object_implied> -- class_creation_implied_object
    | "new" #postWord NCMemberAccessExp ~(NonemptyGenericTypeArgumentList | "(") -- class_creation_no_args
    | LiteralExp

  NCMemberAccessExp = MemberAccessExpNonCall<NCMemberAccessExp>

  ArgumentList
    = Indented<ArgumentList>
    | ListOf<Argument<Expression>, #expressionTerminator>

  ImpliedCallArgumentList
    = Indented<ImpliedCallArgumentList>
    | NonemptyListOf<Argument<NonLoopExpression>, ",">

  Argument<Expr>
    = Indented<Argument<Expr>>
    | Expr
    | "..." Expr -- spread_operator

  Parameter
    = Assignable (":" Type)? "=" Expression -- initializer
    | Assignable "?"? ":" Type -- type
    | Assignable -- assignable

  RestParameter
    = "..." Assignable ":" Type -- with_type
    | "..." Assignable -- without_type

  ParameterList
    = NonemptyListOf<Parameter, #expressionTerminator> (#expressionTerminator RestParameter)? -- params
    | RestParameter -- rest_params

  PostfixExp
    = Indented<PostfixExp>
    | (Accessor | identifier) #(~space) "++" -- increment
    | (Accessor | identifier) #(~space) "--" -- decrement
    | MemberAccessExp

  NotExp
    = Indented<NotExp>
    | "!" NotExp -- logical_not_symbolic
    | "not" #space NotExp -- logical_not_worded
    | "type" "of" #space NotExp -- typeof
    | "await" #space NotExp -- await
    | "++" #(~(space)) (Accessor | identifier) -- prefix_increment
    | "--" #(~(space)) (Accessor | identifier) -- prefix_decrement
    | "+" #(~(space | digit)) NotExp -- unary_plus
    | "-" #(~(space | digit)) NotExp -- unary_minus
    | PostfixExp

  ExpExp
    = Indented<ExpExp>
    | NotExp "^" ExpExp -- exponentiate
    | NotExp

  MulExp
    = Indented<MulExp>
    | MulExp "*" ExpExp -- multiplication
    | MulExp "/" ExpExp -- division
    | MulExp "%" ExpExp -- modulus
    | ExpExp

  AddExp
    = Indented<AddExp>
    | AddExp "+" MulExp -- addition
    | AddExp "-" MulExp -- subtraction
    | MulExp

  BitwiseExp
    = Indented<BitwiseExp>
    | BitwiseExp "<<" AddExp -- left_shift
    | BitwiseExp ">>" AddExp -- right_shift
    | BitwiseExp ">>>" AddExp -- unsigned_right_shift
    | AddExp

  CompareExp = BitwiseExp (comparisonOperator BitwiseExp)*

  comparisonOperator
    = ("==" | "is" ~id_continue) -- equal_to
    | ("!=" | "isnt" ~id_continue) -- not_equal_to
    | "<" -- less_than
    | ">" -- greater_than
    | "<=" -- less_than_equal
    | ">=" -- greater_than_equal
    | "in" postWord -- in
    | "instanceof" -- instanceof

  LogicalAndExp
    = Indented<LogicalAndExp>
    | LogicalAndExp #(spaces "&&" spaces | space+ "and" space+) CompareExp -- logical_and
    | CompareExp

  LogicalOrExp
    = Indented<LogicalOrExp>
    | LogicalOrExp #(spaces "||" spaces | space+ "or" space+) LogicalAndExp -- logical_or
    | LogicalOrExp "??" LogicalAndExp -- logical_nullish_coalescing
    | LogicalAndExp

  TernaryExp
    = Indented<TernaryExp>
    | ("if" | "unless") #postWord TernaryExp "then" #postWord TernaryExp "else" #postWord TernaryExp -- if_then_else
    | LogicalOrExp "?" TernaryExp ":" TernaryExp -- symbolic
    | LogicalOrExp

  postWord = space* ~id_continue | space+
  lineBreak = whitespace? "\n" | whitespace? "#"

  PipeExp
    = Indented<PipeExp>
    | (TernaryExp "|>")+ TernaryExp -- pipe
    | TernaryExp

  AssignmentExp
    = Indented<AssignmentExp>
    | AssignableOrAccessor "=" AssignmentExp -- assignment
    | (Accessor | identifier) "[" Expression? ".." Expression? "]" "=" AssignmentExp -- splice
    | (Accessor | identifier) "[" indent Expression? ".." Expression? dedent "]" "=" AssignmentExp -- splice_indented
    | (Accessor | identifier) ("+=" | "-=" | "*=" | "/=" | "%=" | "^=" | "&&=" | "||=" | "??=" | "and=" | "or=" | "<<=" | ">>=" | ">>>=") AssignmentExp -- update_assignment
    | "yield" #space "from" #postWord AssignmentExp -- yield_from
    | "yield" #space AssignmentExp? -- yield
    | PipeExp

  ForExp
    = Indented<ForExp>
    | AssignmentExp #(~lineBreak) "for" #postWord ("await" #postWord)? (Assignable "of" #postWord)? ForExp 
      ("when" #postWord Expression)? -- for
    | AssignmentExp

  WhileExp
    = Indented<WhileExp>
    | ForExp #(~lineBreak) ("while" | "until") #postWord WhileExp ("when" #postWord Expression)? -- while
    | ForExp

  IfExp
    = Indented<IfExp>
    | WhileExp #(~lineBreak) ("if" | "unless") #postWord IfExp -- if
    | WhileExp

  VariableAssignment = Assignable (":" Type)? "=" Expression
  ExportedVariableAssignment = "export" #space Assignable (":" Type)? "=" Expression
  TypedVariableAssignment = Assignable ":" Type "=" Expression

  AssignableOrAccessor = Accessor | Assignable ~("." | "[")

  AssignableWithDefault
    = Assignable "=" Expression -- with_default
    | Assignable

  AssignableKeyWithRewrite
    = MethodName ":" AssignableWithDefault -- rewrite
    | AssignableWithDefault

  Assignable
    = identifier -- identifier
    | "[" ListOf<AssignableWithDefault, #expressionTerminator>
      (#expressionTerminator "..." Assignable)? ","? "]" -- array
    | "{" ListOf<AssignableKeyWithRewrite, #expressionTerminator>
      (#expressionTerminator "..." Assignable)? ","? "}" -- object

  Accessor
    = (identifier | Property) AccessorAddon+
    | Property AccessorAddon*

  AccessorAddon
    = "." word -- member_access
    | "[" Expression "]" -- computed_member_access

  whitespace = (~("\n" | line_comment | block_comment) space)+
  indent = "⇨"
  dedent = "⇦"

  sign = ("+" | "-")?
  number = digit+ ("_" digit+)*
  fullNumber = sign number ("." number)? (("e" | "E") sign number)?
  unitNumber = ~"0x" fullNumber ~("n" ~id_continue | "_") identifier
  decimalNumber = ~"0x" fullNumber ~char
  hexNumber = sign "0x" hexDigit+ ("_" hexDigit+)*
  bigint = ~"0x" sign number "n"

  boolean = "true" | "false" | "yes" | "no" | "on" | "off"
  null = "null"
  undefined = "undefined"

  id_continue = alnum | "_"

  string_bit<delimeter>
    = ~("\\" | "#{" | delimeter) any -- character
    | "\\" ~("b" | "f" | "n" | "r" | "t" | "u" | "v" | "x" | "0") any -- escape
    | "\\" ("b" | "f" | "n" | "r" | "t" | "v" | ("0" ~digit)) -- escape_sequence
    | "\\x" hexDigit hexDigit -- hex_sequence
    | "\\u" hexDigit hexDigit hexDigit hexDigit -- unicode_sequence
    | "\\u{" hexDigit+ "}" -- unicode_code_point_sequence

  string_interpolatable_head<delimeter> = string_bit<delimeter>*

  string_interpolatable_span<delimeter, interpolated>
    = "#{" interpolated "}" string_bit<delimeter>* &(delimeter?)

  string_interpolatable<delimeter, interpolated>
    = delimeter string_interpolatable_head<delimeter>
      "" string_interpolatable_span<delimeter, interpolated>* delimeter

  string_full<delimeter> = delimeter string_bit<delimeter>* delimeter

  string_non_interpolatable
    = string_full<"\"\"\"">
    | string_full<"'''">
    | string_full<"\"">
    | string_full<"'">

  string
    = string_interpolatable<"\"\"\"", applySyntactic<Expression>>
    | string_interpolatable<"'''", applySyntactic<Expression>>
    | string_interpolatable<"\"", applySyntactic<Expression>>
    | string_interpolatable<"'", applySyntactic<Expression>>

  string_type
    = string_interpolatable<"\"\"\"", applySyntactic<Type>>
    | string_interpolatable<"'''", applySyntactic<Type>>
    | string_interpolatable<"\"", applySyntactic<Type>>
    | string_interpolatable<"'", applySyntactic<Type>>

  reserved_block = ("if" | "else" | "then" | "throw" | "for" | "while" | "until" | "unless" | "do" ~"wn" | "return" | "break" | "continue" | "import" | "export" | "class" | "try" | "catch" | "finally" | "switch" | "case" | "default" | "rescope" | "down" | "type" | "interface" | "namespace" | "with" | "when") ~id_continue
  reserved_operators = ("and" | "or" | "not" | "is" ~"nt" | "isnt") ~id_continue
  reserved_inline = ("new" | "in" | "type" ~"of" | "of" | "from" | "typeof" | "as" | "extends" | "await" | "readonly" | "infer") ~id_continue
  reserved_primitive = ("true" | "false" | "yes" | "no" | "on" | "off" | "null" | "undefined") ~id_continue

  // `super` and `this` are omitted because they are valid names and used often.
  reserved_javascript = ("break" | "case" | "catch" | "class" | "const" | "continue" | "debugger" | "default" | "delete" | "do" | "else" | "enum" | "export" | "extends" | "false" | "finally" | "for" | "function" | "if" | "import" | "in" ~"stanceof" | "instanceof" | "new" | "null" | "return" | /* "super" | */ "switch" | /* "this" | */ "throw" | "true" | "try" | "typeof" | "var" | "void" | "while" | "with" | "implements" | "interface" | "let" | "package" | "private" | "protected" | "public" | "static" | "yield") ~id_continue
  reserved = reserved_block | reserved_operators | reserved_inline | reserved_primitive | reserved_javascript

  char = letter | "_"
  word = char+ (number char*)*

  identifier = ~reserved word

  Property
    = "@" #word -- identifier
    | "@" #"[" Expression "]" -- computed

  space += block_comment | line_comment
  block_comment = "/*" (~"*/" any)* "*/"
  line_comment = "#" (~("\n" | dedent) any)*

  JSXElement
    = "<" #jsxTagName GenericTypeArgumentList JSXAttribute* ">" JSXChild* "</" #jsxTagName ">" -- open_close
    | "<" #jsxTagName GenericTypeArgumentList JSXAttribute* "/" ">" -- self_closing

  jsx_string = (~("<" | "{") any)+

  JSXChild
    = "{" "..."? Expression "}" -- interpolation
    | JSXElement
    | jsx_string

  JSXAttribute
    = JSXAttributeKey ~"=" -- value_true
    | JSXAttributeKey "=" string -- value_computed_string
    | JSXAttributeKey "=" "{" Expression "}" -- value_expression
    | "{" "..." Expression "}" -- spread_attributes

  JSXAttributeKey = word

  jsxTagName
    = jsxTagName "." word -- property_access
    | word -- standard

  Type = IfType
  NonLoopType = MappedType

  FunctionReturnType
    = Indented<FunctionReturnType>
    | ("asserts" #space)? identifier "is" #postWord NonLoopType -- predicate
    | NonLoopType

  PrimitiveType
    = Indented<PrimitiveType>
    | "string" | "number" | "boolean" | "true" | "false" | "yes" | "no" | "on" | "off" | "this"
    | "bigint" | "symbol" | "object" | "null" | "undefined" | "void" | "any" | "unknown"

  Implementable
    = identifier ("." word)* GenericTypeArgumentList &("," | "implements" | "{")

  Extendable
    = identifier ("." word)* NonemptyGenericTypeArgumentList &("," | "implements" | "{")

  QualifiedName = identifier ("." word)*

  LiteralType
    = Indented<LiteralType>
    | PrimitiveType ~(id_continue | ".")
    | string_type
    | decimalNumber
    | bigint
    | FunctionType
    | "infer" #space identifier (":" #space Type)? -- infer
    | "typeof" #space QualifiedName -- typeof
    | "new" FunctionType -- construct
    | QualifiedName ~"<"
    | QualifiedName NonemptyGenericTypeArgumentList -- type_args
    | "(" Type ")" -- parenthesized

  MemberAccessType
    = Indented<MemberAccessType>
    | MemberAccessType "[" Type "]" -- member_access
    | MemberAccessType "[" "]" -- array
    | "[" ListOf<NamedTupleElement, #expressionTerminator> ","? "]" -- named_tuple
    | "[" ListOf<TupleElement, #expressionTerminator> ","? "]" -- tuple
    | "{" ListOf<TypeObjectEntry, #typeTerminator> ("," | ";")? "}" -- object
    | NonemptyListOf<TypeObjectEntry_key_value, ("," | ";" | #lineBreak)> -- object_implied
    | "readonly" ~(#id_continue) MemberAccessType -- readonly
    | "keyof" ~(#id_continue) MemberAccessType -- keyof
    | LiteralType

  NamedTupleElement
    = identifier "?"? ":" Type -- name_value
    | "..." identifier ":" Type -- spread_operator

  TupleElement
    = Type "?"? -- value
    | "..." Type -- spread_operator

  TypeObjectKey
    = word -- identifier
    | string_non_interpolatable -- string
    | decimalNumber -- numerical_key
    | "[" Accessor "]" -- computed_accessor

  TypeObjectEntry
    = ("readonly" #space)? TypeObjectKey "?"? ":" Type -- key_value
    | "new" FunctionType -- construct_signature
    | FunctionType -- call_signature
    | IndexSignatureType<"">

  TypeObjectEntry_implied = ("readonly" #space)? TypeObjectEntry "?"? ":" NonLoopType

  FunctionType
    = (GenericTypeParameterList? "(" ParameterList? ")")?
      "->" (#(~(lineBreak ~indent)) FunctionReturnType)?

  IntersectionType = Indented<IntersectionType> | NonemptyListOf<MemberAccessType, "&">
  UnionType = Indented<UnionType> | NonemptyListOf<IntersectionType, "|">

  ConditionalType
    = Indented<ConditionalType>
    | ConditionalType "is" #postWord ConditionalType
      "?" ConditionalType ":" ConditionalType -- conditional
    | ("if" | "unless") #postWord ConditionalType "is" #postWord ConditionalType
      "then" #postWord ConditionalType "else" #postWord ConditionalType -- if_then_else
    | UnionType

  MappedType
    = Indented<MappedType>
    | "{" (("+" | "-")? "readonly")? "[" identifier "in" Type
      ("as" Type)? "]" (("+" | "-")? "?")? ":" Type "}" -- mapped
    | ConditionalType

  IfType
    = Indented<IfType>
    | MappedType ("if" | "unless") #postWord IfType "is" #postWord IfType -- if
    | MappedType

  NamespaceDeclaration = ("export" #space)? "namespace" #space identifier WrappedScriptBlock

  InterfaceDeclaration
    = ("export" #space)? "interface" #space identifier GenericTypeParameterList?
      (#space "extends" #space NonemptyListOf<Implementable, #expressionTerminator>)?
      "{" ListOf<TypeObjectEntry, #typeTerminator> "}"
    | ("export" #space)? "interface" #space identifier GenericTypeParameterList?
      (#space "extends" #space NonemptyListOf<Implementable, #expressionTerminator>)?
      /**/ indent NonemptyListOf<TypeObjectEntry, #typeTerminator> dedent

  IndexSignatureType<prefix> = "readonly"? prefix "[" identifier ":" Type "]" ":" Type

  TypeDeclaration
    = ("export" #space)? "type" #space identifier GenericTypeParameterList? "=" Type #statementTerminator

  GenericTypeParameter
    = Indented<GenericTypeParameter>
    | identifier (":" Type)? ("=" Type)? -- parameter
    | identifier indent (":" Type)? ("=" Type)? dedent -- indented

  GenericTypeParameterList
    = "<" NonemptyListOf<GenericTypeParameter, #expressionTerminator> ">"
    | "<" Indented<NonemptyListOf<GenericTypeParameter, #expressionTerminator>> ">"

  NonemptyGenericTypeArgumentList
    = "<" NonemptyListOf<Type, #expressionTerminator> ">"
    | "<" Indented<NonemptyListOf<Type, #expressionTerminator>> ">"

  GenericTypeArgumentList
    = (Indented<GenericTypeArgumentList> | NonemptyGenericTypeArgumentList) -- with_args
    | -- empty
}
